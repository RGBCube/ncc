let
  hickoryModule =
    { lib, ... }:
    let
      inherit (lib.lists) singleton;
    in
    {
      services.hickory-dns = {
        enable = true;

        settings = {
          listen_port = 53;
          listen_addrs_ipv6 = singleton "::";

          zones = singleton {
            zone = ".";
            zone_type = "External";

            stores.type = "forward";
            stores.name_servers = singleton {
              ip = "2a07:a8c0::7f:2bf8";
              trust_negative_responses = true;
              connections = [
                {
                  protocol.server_name = "dns.nextdns.io";
                  protocol.path = "/7f2bf8";
                  protocol.type = "h3"; # http3
                }
                {
                  protocol.server_name = "dns.nextdns.io";
                  protocol.path = "/7f2bf8";
                  protocol.type = "https";
                }
                {
                  protocol.server_name = "dns.nextdns.io";
                  protocol.type = "quic";
                }
                {
                  protocol.server_name = "dns.nextdns.io";
                  protocol.type = "tls";
                }
              ];
            };
          };
        };
      };
    };
in
{
  nixosModules.dns =
    { config, lib, ... }:
    let
      inherit (lib.lists) head;
    in
    {
      imports = [ hickoryModule ];

      etc."resolv.conf".text = # nginx
        ''
          # Generated by NixOS/nix-darwin modules, should use the Hickory-DNS forwarder.
          options edns0 trust-ad
          nameserver ${head config.services.hickory-dns.settings.listen_addrs_ipv6}
        '';
    };

  darwinModules.dns =
    { config, lib, ... }:
    let
      inherit (lib.lists) head singleton;
    in
    {
      imports = [ hickoryModule ];

      networking.dns = singleton <| head config.services.hickory-dns.settings.listen_addrs_ipv6;
    };
}
